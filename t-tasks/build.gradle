apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'
apply plugin: 'me.tatarka.retrolambda'
//apply plugin: 'com.getkeepsafe.dexcount'
//apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
    defaultConfig {
        applicationId "com.teo.ttasks"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName '0.1'
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        release {
            storeFile file('keystore/release.jks')
            keyAlias 'teo baranga'
            keyPassword "$System.env.TT_KEY"
            storePassword "$System.env.TT_KEY"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            resValue "string", "app_name", "T-Tasks Dev"
        }
        prod {
            resValue "string", "app_name", "T-Tasks"
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a', 'mips'
            universalApk false
        }
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        warningsAsErrors true
        abortOnError true // Fail early.

        disable 'GoogleAppIndexingWarning' // App does not have deep linking.
        disable 'InvalidPackage' // Okio references java.nio that does not presented in Android SDK.
    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    // This allows us see tests execution progress in the output on the CI.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat 'full'
        }
    }
}

dependencies {
    compile libraries.dagger
    apt libraries.daggerCompiler

    compile libraries.rxJava
    compile libraries.rxAndroid

    compile libraries.supportDesign
    compile libraries.supportFragment

    compile libraries.timber

    compile libraries.playServicesAuth

    compile libraries.retrofit
    compile libraries.retrofitGson
    compile libraries.retrofitRxJavaAdapter

    compile libraries.okHttpLoggingInterceptor

    compile('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.google.guava', module: 'guava-jdk5'
        exclude group: 'com.google.android', module: 'android'
        exclude group: 'junit'
        exclude group: 'xpp3'
    }
    compile('com.mikepenz:aboutlibraries:5.8.1@aar') { transitive = true }
    compile('com.mikepenz:fastadapter:1.8.0@aar') { transitive = true }
    compile('com.mikepenz:materialdrawer:5.6.0@aar') { transitive = true }
    compile 'com.mikepenz:google-material-typeface:2.2.0.1@aar'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.artemzin.rxjava:proguard-rules:1.1.10.0'
    compile 'com.github.strooooke:appbarsyncedfab:v0.3'

    apt 'org.projectlombok:lombok:1.16.10'

    provided 'org.projectlombok:lombok:1.16.10'
    provided 'javax.annotation:jsr250-api:1.0'

    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

    // Dependencies for local unit tests
    testCompile libraries.junit
    testCompile "org.mockito:mockito-all:${mockitoVersion}"
    testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testCompile "org.powermock:powermock-module-junit4:${powerMockito}"
    testCompile "org.powermock:powermock-api-mockito:${powerMockito}"

    // Android Testing Support Library's runner and rules
    androidTestCompile libraries.supportTestRunner
    androidTestCompile libraries.supportTestRules

    // Espresso UI Testing dependencies.
    androidTestCompile libraries.espressoCore
    androidTestCompile(libraries.espressoContrib) {
        // Causes IncompatibleClassChangeError. As alwaysâ€¦
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
    resolutionStrategy {
        // Force our version of support-annotations, we have a conflict between app and androidTest dependency resolution.
        force libraries.supportAnnotations
        force "com.google.code.findbugs:jsr305:2.0.1"
    }
}

/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}

// This needs to be at the bottom so that no dependency collisions are introduced
apply plugin: 'com.google.gms.google-services'
